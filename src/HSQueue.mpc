// Copyright (c) 2014, Object Computing, Inc.
// All rights reserved.
// See the file license.txt for licensing information.

//////////////////////////////
// Build the HSQueue library
project(*) {
// : boost_base, boost_filesystem, boost_system, boost_thread {
  sharedname = *
//  staticname = HSQueue
  dynamicflags += HSQUEUE_BUILD_DLL
  includes += $(HSQueue_ROOT)/src
  pch_header = Common/HSQueuePch.h
  pch_source = Common/HSQueuePch.cpp
  Source_Files {
	HSQueue
	HSQueue/details
	Common
  }
  Header_Files {
	HSQueue
	HSQueue/details
	Common
  }

  specific(prop:microsoft) {
    Release::libout = $(HSQueue_ROOT)/Output/Release
    Debug::libout = $(HSQueue_ROOT)/Output/Debug
  } else {
    libout = $(HSQueue_ROOT)/lib
  }

  specific(make) {
    // Enable full optimization on gcc/linux
    Release::genflags += -O3
  }
}

////////////////////////////
// Build the HSQueue tests
project(*test) : boost_unit_test_framework, boost_base, boost_filesystem, boost_system, boost_thread {
  exename = HSQueueTest
  includes += $(HSQueue_ROOT)/src

  specific(prop:microsoft) {
    Release::exeout = $(HSQueue_ROOT)/Output/Release
    Debug::exeout = $(HSQueue_ROOT)/Output/Debug
    Release::libpaths += $(HSQueue_ROOT)/Output/Release
    Debug::libpaths += $(HSQueue_ROOT)/Output/Debug
  } else {
    libpaths += $(HSQueue_ROOT)/lib
    exeout = $(HSQueue_ROOT)/bin
  }
  // TODO: What to do about boost 32 bit builds?
  libpaths -= $(BOOST_ROOT)/lib
  libpaths += $(BOOST_LIB_PATH)

  libs += HSQueue
  after += HSQueue
  macros += BOOST_TEST_DYN_LINK
  pch_header = Common/HSQueuePch.h
  pch_source = Common/HSQueuePch.cpp
  Source_Files {
    HSQTest
  }
  Header_Files {
    HSQTest
	Common
  }
}

////////////////////////////
// Build the HSQueue performance tests
project(*performance) : boost_unit_test_framework, boost_base, boost_filesystem, boost_system, boost_thread {
  exename = HSQueuePerformance
  includes += $(HSQueue_ROOT)/src

  specific(prop:microsoft) {
    Release::exeout = $(HSQueue_ROOT)/Output/Release
    Debug::exeout = $(HSQueue_ROOT)/Output/Debug
    Release::libpaths += $(HSQueue_ROOT)/Output/Release
    Debug::libpaths += $(HSQueue_ROOT)/Output/Debug
  } else {
    libpaths += $(HSQueue_ROOT)/lib
    exeout = $(HSQueue_ROOT)/bin
  }
  // TODO: What to do about boost 32 bit builds?
  libpaths -= $(BOOST_ROOT)/lib
  libpaths += $(BOOST_LIB_PATH)
  
  libs += HSQueue
  after += HSQueue
  macros += BOOST_TEST_DYN_LINK
  pch_header = Common/HSQueuePch.h
  pch_source = Common/HSQueuePch.cpp
  Source_Files {
    HSQPerformance
  }
  Header_Files {
    HSQPerformance
	Common
  }
}
